## -*- tcl -*-
## (c) 2013 Andreas Kupries
# # ## ### ##### ######## ############# #####################
## Test suite for memory stores, custom parts.
#
## The external procedures 'already' and 'badmethod' are used to
## generate implementation specific error messages. Implementations
## have to be provided by the invoking implementation-specific
## testsuite.
#
## The external procedures 'new-store' and 'release-store' are used to
## create and destroy an instance of the store under test.

# # ## ### ##### ######## ############# #####################

test [test-class]-1.0 {new, wrong#args, too many} -body {
    atom::memory new X
} -returnCodes error -result {wrong # args: should be "atom::memory new"}

test [test-class]-1.1 {create, wrong#args, too many} -body {
    atom::memory create foo X
} -returnCodes error -result {wrong # args: should be "atom::memory create foo"}

test [test-class]-1.2 {constructor, create, existing command} -setup {
    atom::memory create test-memory-store
} -body {
    atom::memory create test-memory-store
} -cleanup {
    test-memory-store destroy
} -returnCodes error -result [already test-memory-store]

test [test-class]-1.3 {constructor, create, existing command} -body {
    atom::memory create set
} -returnCodes error -result [already set]

test [test-class]-1.4 {constructor, create} -setup {
    set result {}
} -body {
    lappend result [atom::memory create test-memory-store]
    lappend result [info commands ::test-memory-store]
    lappend result [test-memory-store size]
} -cleanup {
    test-memory-store destroy
    unset result
} -result {::test-memory-store ::test-memory-store 0}

test [test-class]-1.5 {constructor, new} -setup {
    set result {}
} -body {
    lappend result [set foo [atom::memory new]]
    lappend result [info commands ::$foo]
    lappend result [$foo size]
} -cleanup {
    $foo destroy
    unset result foo
} -match glob -result {::oo::Obj* ::oo::Obj* 0}

# # ## ### ##### ######## ############# #####################
return
